#version 450 core

//#define INDIRECT_SHADOW

#include "globalubos.glsl"
#include "utils.glsl"
#include "lightingfunctions.glsl"
#define LIGHTCACHEMODE LIGHTCACHEMODE_LIGHT
#include "lightcache.glsl"

layout(binding=0) uniform sampler2D RSM_Flux;
layout(binding=1) uniform sampler2D RSM_DepthLinSq;
layout(binding=2) uniform isampler2D RSM_Normal;

#ifdef INDIRECT_SHADOW
	layout(binding=4) uniform sampler3D VoxelVolume;
#endif

#ifdef INDIRECT_SPECULAR
	// Needs to be defined:
	//#define SPECULARENVMAP_PERCACHESIZE

	// Optional: Writes to the specular environment map directly, instead of trying to keep results compress in registers.
	//#define DIRECT_SPECULAR_MAP_WRITE

	#ifdef DIRECT_SPECULAR_MAP_WRITE
	layout(binding = 0, r11f_g11f_b10f) restrict uniform image2D CacheSpecularEnvmap;
	#else
	layout(binding = 0, r11f_g11f_b10f) restrict writeonly uniform image2D CacheSpecularEnvmap;
	#endif

#endif

struct LightInfo
{
	vec3 Flux;
	float DiscArea;
	vec3 Position;
	vec3 Normal;
};
shared LightInfo RSMCache[LIGHTING_THREADS_PER_GROUP];


// Morton helper - http://and-what-happened.blogspot.de/2011/08/fast-2d-and-3d-hilbert-curves-and.html
// unpack 2 16-bit indices from a 32-bit Morton code
uvec2 Morton_2D_Decode_16bit(in uint morton)
{
	uvec2 coord;
	coord.x = morton;
	coord.y = ( coord.x >> 1 );
	coord &= 0x55555555;
	coord |= ( coord >> 1 );
	coord &= 0x33333333;
	coord |= ( coord >> 2 );
	coord &= 0x0f0f0f0f;
	coord |= ( coord >> 4 );
	coord &= 0x00ff00ff;
	coord |= ( coord >> 8 );
	coord &= 0x0000ffff;
	
	return coord;
}


uint EndcodeRadiance(vec3 radianceIn)
{
	vec4 sharedExponent;
	float maxVal = max(radianceIn.r, max(radianceIn.g, radianceIn.b));
	sharedExponent.a = ceil(log2(maxVal));
	sharedExponent.rgb = saturate(radianceIn / exp2(sharedExponent.a));
	sharedExponent.a = (sharedExponent.a + 128.0) / 255.0;
	return packUnorm4x8(sharedExponent);
}
vec3 DecodeRadiance(uint encodedradianceIn)
{
	vec4 sharedExponent = unpackUnorm4x8(encodedradianceIn);
	return sharedExponent.rgb * exp2(sharedExponent.a * 255.0 - 128.0);
}

layout (local_size_x = LIGHTING_THREADS_PER_GROUP, local_size_y = 1, local_size_z = 1) in;
void main()
{
	vec3 worldPosition = LightCacheEntries[gl_GlobalInvocationID.x].Position;
	vec3 toCamera = normalize(CameraPosition - worldPosition);


#ifdef INDIRECT_SPECULAR
	ivec2 cacheSpecularEnvmapOffset = ivec2(gl_GlobalInvocationID.x % SpecularEnvmapNumCachesPerDimension, 
										gl_GlobalInvocationID.x / SpecularEnvmapNumCachesPerDimension) * SPECULARENVMAP_PERCACHESIZE;

	#ifndef DIRECT_SPECULAR_MAP_WRITE
	uint localSpecularEnvmap[SPECULARENVMAP_PERCACHESIZE * SPECULARENVMAP_PERCACHESIZE];
	for(int i=0; i<SPECULARENVMAP_PERCACHESIZE * SPECULARENVMAP_PERCACHESIZE; ++i)
		localSpecularEnvmap[i] = 0;
	#endif

	float specularEnvMapBaseBlinnExp = GetBlinnPhongExponentForHemispherePixel(SPECULARENVMAP_PERCACHESIZE);
	float specularEnvMapBaseNormalization = BlinnNormalization(specularEnvMapBaseBlinnExp);
#endif


#ifdef INDIRECT_SHADOW
	ivec3 volumeSize = textureSize(VoxelVolume, 0);
	vec3 voxelPos = (worldPosition - VolumeWorldMin) / (VoxelSizeInWorld * volumeSize);
	float startOffset = sqrt(2.0) * AddressVolumeCascades[0].WorldVoxelSize / VoxelSizeInWorld + 0.5;
#endif


#if defined(INDDIFFUSE_VIA_SH1) || defined(INDDIFFUSE_VIA_SH2)
	vec3 SH00 = vec3(0);
	vec3 SH1neg1 = vec3(0);
	vec3 SH10 = vec3(0);
	vec3 SH1pos1 = vec3(0);
	#ifdef INDDIFFUSE_VIA_SH2
	vec3 SH2neg2 = vec3(0);
	vec3 SH2neg1 = vec3(0);
	vec3 SH20 = vec3(0);
	vec3 SH2pos1 = vec3(0);
	vec3 SH2pos2  = vec3(0);
	#endif
#endif

#if defined(INDIRECT_SPECULAR) || defined(INDDIFFUSE_VIA_H)
	mat3 cacheLocalViewSpace = ComputeLocalViewSpace(worldPosition);
#endif
#ifdef INDDIFFUSE_VIA_H
	vec3 irradianceHBasis[INDDIFFUSE_VIA_H];
	for(int i=0; i<INDDIFFUSE_VIA_H; ++i)
		irradianceHBasis[i] = vec3(0);
#endif

	int totalNumRSMPixels = ShadowMapResolution * ShadowMapResolution;
	for(uint rsmPixelIndex = 0; rsmPixelIndex < totalNumRSMPixels; rsmPixelIndex += LIGHTING_THREADS_PER_GROUP)
	{
		// Load LIGHTING_THREADS_PER_GROUP RMS pixels
		LightInfo cacheEntry;

		// Unpack rsmPixelIndex to a actual position.
		uint localRsmPixelPos = rsmPixelIndex + gl_LocalInvocationID.x;
		ivec2 rsmSamplePos = ivec2(Morton_2D_Decode_16bit(localRsmPixelPos)); 
		//ivec2 rsmSamplePos = ivec2(localRsmPixelPos / ShadowMapResolution, localRsmPixelPos % ShadowMapResolution);
		vec2 rsmSamplePosF = (rsmSamplePos + vec2(0.5)) / ShadowMapResolution;

		// Sample flux
		cacheEntry.Flux = textureLod(RSM_Flux, rsmSamplePosF, 0).rgb;

		// Sample Depth and compute VAL area
		float sourceLightToVAL = textureLod(RSM_DepthLinSq, rsmSamplePosF, 0).r;
		cacheEntry.DiscArea = sourceLightToVAL * sourceLightToVAL * ValAreaFactor; // Estimate size of virtual area light

		// Compute world position.
		vec4 rsmClipSpace = vec4(rsmSamplePosF * 2.0 - vec2(1.0), 0.0, 1.0) * InverseLightViewProjection;
		cacheEntry.Position = LightPosition + normalize(rsmClipSpace.xyz / rsmClipSpace.w - LightPosition) * sourceLightToVAL;

		// Sample and unpack Normal
		cacheEntry.Normal = UnpackNormal16I(textureLod(RSM_Normal, rsmSamplePosF, 0).xy);

		// Write into cache, all together.
		barrier(); // Wait for other threads to chew their lights
 		RSMCache[gl_LocalInvocationID.x] = cacheEntry;
		barrier(); // Make sure all other threads have written their lights.

		// Shadow value changes only every SHADOW_COMPUTATION_INTERVAL_BLOCK samples.
		float shadowing = 1.0;

		for(int i=0; i<LIGHTING_THREADS_PER_GROUP; ++i)
		{
		#ifdef INDIRECT_SHADOW
			if(i % IndirectShadowComputationSampleInterval == 0)
			{
				uint localRsmPixelPos = uint(rsmPixelIndex + i);
				uvec2 upperLeftSampleTexel = Morton_2D_Decode_16bit(localRsmPixelPos);
				vec2 rsmMidTexcoord = (upperLeftSampleTexel + IndirectShadowSamplingOffset) / ShadowMapResolution;

				vec2 d_dsq = textureLod(RSM_DepthLinSq, rsmMidTexcoord, IndirectShadowComputationLod).xy;
				vec4 rsmClipSpace = vec4(rsmMidTexcoord * 2.0 - vec2(1.0), 0.0, 1.0) * InverseLightViewProjection;
				vec3 averageValPos = LightPosition + normalize(rsmClipSpace.xyz / rsmClipSpace.w - LightPosition) * d_dsq.x;

				// Compute angle needed for this sample.
				// For this we estimate the sphere containing most samples:
				float averageDepthSq = d_dsq.x * d_dsq.x;
				float depthVariance = d_dsq.y - d_dsq.x * d_dsq.x;
				
				// halfConeAngle = asin(sampleSphereRadius / d_dsq.x)
				// For cone tracing we need a conversion of dist to sphere: radius = dist * sin(halfConeAngle)
				// -> distToSphereRad = sin(halfConeAngle) = sampleSphereRadius / d_dsq.x

				//float sampleSphereRadius = max(d_dsq.x * IndirectShadowComputationSuperValWidth, // Estimate super-val width
				//								sqrt(depthVariance) * 2.0); // Assuming normation distribuation of samples in depth -  Depth deviation * 2.0 -> 95% of all samples

				//float distToSphereRad = sampleSphereRadius / d_dsq.x;
				// Simplification yields:
				float distToSphereRad = max(IndirectShadowComputationSuperValWidth, sqrt(depthVariance) * 2.0 / d_dsq.x);
				distToSphereRad = max(distToSphereRad, IndirectShadowSamplingMinDistToSphereFactor); // Clamp to small values.


				vec3 toAverageVal = averageValPos - worldPosition;
				float lightDist = length(toAverageVal);
				toAverageVal /= lightDist;
				vec3 dirInVoxel = toAverageVal / volumeSize;

				vec3 currentPosVoxel = voxelPos + dirInVoxel*startOffset;
				//float lastOcclusion = 0.0;
				float occlusion = 0.0;
				float stepSize = 1.0;
				float dist = 0.0;
				float goalDist = lightDist / VoxelSizeInWorld - startOffset;// Assuming that the light is within the voxel bounds!

				for(int s=0; s<64; ++s)
				{
					currentPosVoxel += dirInVoxel * stepSize;
					//if(saturate(currentPosVoxel) != currentPosVoxel) break; // seems not to be necessary

					dist += stepSize;

					float currentSphereRadius = dist * distToSphereRad;

					// Lookup in miplevel that has voxels of the size of the current sphere radius.
					float newOcclusion = textureLod(VoxelVolume, currentPosVoxel, log2(currentSphereRadius)).r; 
					//lastOcclusion = occlusion;
					occlusion += (1.0 - occlusion) * newOcclusion;

					if(dist >= goalDist)
						break;

					stepSize = currentSphereRadius * 2.0;
				}

				//occlusion = mix(lastOcclusion, occlusion, (dist - goalDist) / stepSize); // should be correcter but yields more artefacts.
				shadowing = saturate(1.0 - occlusion);
			}
		#endif


			{
				vec3 valTotalExitantFlux = RSMCache[i].Flux;
				if(valTotalExitantFlux.x + valTotalExitantFlux.y + valTotalExitantFlux.z < 0.001) continue;

				vec3 valNormal = RSMCache[i].Normal;
				vec3 valPosition = RSMCache[i].Position;


				// Direction and distance to light.
				vec3 toVal = valPosition.xyz - worldPosition;
				//if(dot(toVpl, worldNormal) > 0) // Early out if facing away from the light. 
				//	continue;
				float lightDistanceSq = dot(toVal, toVal);
				toVal *= inversesqrt(lightDistanceSq);

				// Light intensity
				float fluxToIntensity = saturate(dot(valNormal, -toVal)) / PI;

				fluxToIntensity *= shadowing;

				// Note that this is not yet irradianceIn - multiplication with dot(N,L) is missing.
				vec3 radianceIn = valTotalExitantFlux * (fluxToIntensity / (lightDistanceSq + RSMCache[i].DiscArea));
				//vec3 radianceIn = valTotalExitantFlux * (fluxToIntensity / lightDistanceSq); // VPL instead of VAL

			#if defined(INDDIFFUSE_VIA_SH1) || defined(INDDIFFUSE_VIA_SH2)
				// SH IrradianceIn
				SH00 += ShCosLobeFactor0 * radianceIn;
				SH1neg1 -= (ShCosLobeFactor1 * toVal.y) * radianceIn;
				SH10 += (ShCosLobeFactor1 * toVal.z) * radianceIn;
				SH1pos1 -= (ShCosLobeFactor1 * toVal.x) * radianceIn;

				#ifdef INDDIFFUSE_VIA_SH2
					SH2neg2 -= (ShCosLobeFactor2n2_p1_n1 * toVal.x * toVal.y) * radianceIn;
					SH2neg1 += (ShCosLobeFactor2n2_p1_n1 * toVal.y * toVal.z) * radianceIn;
					SH20 += (ShCosLobeFactor20 * (toVal.z * toVal.z * 3.0 - 1.0)) * radianceIn;
					SH2pos1 += (ShCosLobeFactor2n2_p1_n1 * toVal.x * toVal.z) * radianceIn;
					SH2pos2 += (ShCosLobeFactor2p2 * (toVal.x * toVal.x - toVal.y * toVal.y)) * radianceIn;
				#endif
			#endif

			#if defined(INDIRECT_SPECULAR) || defined(INDDIFFUSE_VIA_H)
				vec3 toValCacheLocal = toVal * cacheLocalViewSpace;
			#endif

			#ifdef INDDIFFUSE_VIA_H
				// H-Basis Irradiance
				// The H-Basis version is comparatively unoptimized since the visual results were not as good:
				// It leads to "swimming" of the lighting which makes it rather unsuitable.
				float SH00 = ShCosLobeFactor0;
				float SH1neg1 = -ShCosLobeFactor1  * toValCacheLocal.y;
				float SH10 = ShCosLobeFactor1  * toValCacheLocal.z;
				float SH1pos1 = -ShCosLobeFactor1 * toValCacheLocal.x;
				float SH2neg2 = -ShCosLobeFactor2n2_p1_n1 * toValCacheLocal.x * toValCacheLocal.y;
				float SH2neg1 = ShCosLobeFactor2n2_p1_n1 * toValCacheLocal.y * toValCacheLocal.z;
				float SH20 = ShCosLobeFactor20 * (toValCacheLocal.z * toValCacheLocal.z * 3.0 - 1.0);
				float SH2pos1 = ShCosLobeFactor2n2_p1_n1  * toValCacheLocal.x * toValCacheLocal.z;
				float SH2pos2 = ShCosLobeFactor2p2  * (toValCacheLocal.x*toValCacheLocal.x - toValCacheLocal.y*toValCacheLocal.y);

				irradianceHBasis[0] += (1.0 / sqrt(2) * SH00 		+ 0.5 * sqrt(3.0/2.0) * SH10) * radianceIn; 
				irradianceHBasis[1] += (1.0 / sqrt(2) * SH1neg1 	+ 3.0 / 8.0 * sqrt(5.0/2.0) * SH2neg1) * radianceIn; 
				irradianceHBasis[2] += (1.0 / (2.0*sqrt(2)) * SH10 	+ 1.0 / 4.0 * sqrt(15.0/2.0) * SH20) * radianceIn; 
				irradianceHBasis[3] += (1.0 / sqrt(2) * SH1pos1 	+ 3.0 / 8.0 * sqrt(5.0/2.0) * SH2pos1) * radianceIn;
				#if INDDIFFUSE_VIA_H > 4
				irradianceHBasis[4] += (1.0 / sqrt(2) * SH2neg2) * radianceIn; 
				irradianceHBasis[5] += (1.0 / sqrt(2) * SH2pos2) * radianceIn; 
				#endif
			#endif


			#ifdef INDIRECT_SPECULAR
				// Cache local half vector - in cache local 0,0,1 is the direction to the camera!
				// Result is definitively z positive now.
				vec3 halfVecCacheLocal = normalize(toValCacheLocal + vec3(0,0,1));
				ivec2 cacheSpecEnvmapTexelCoord = ivec2(HemisphericalProjection(halfVecCacheLocal) * SPECULARENVMAP_PERCACHESIZE + 0.5);	

				// We want to save reflected radianceIn!
				// Lout = BlinnNormalization * (n dot h)^BlinnExp * (n dot l) * Lin
				// Assumption: As we save only the lowest mip level, the Blinn exponent is so high, that all normals != h are not affected.
				// -> n = h
				// -> Lout = BlinnNormalization * (h dot l) * Lin
				vec3 radianceOut = radianceIn * saturate(dot(halfVecCacheLocal, toValCacheLocal));

				#ifdef DIRECT_SPECULAR_MAP_WRITE
					// Normalization is applied right away.
					radianceOut *= specularEnvMapBaseNormalization;
					cacheSpecEnvmapTexelCoord += cacheSpecularEnvmapOffset;
					memoryBarrierImage();
					vec3 oldradianceIn = imageLoad(CacheSpecularEnvmap, cacheSpecEnvmapTexelCoord).rgb;
					imageStore(CacheSpecularEnvmap, cacheSpecEnvmapTexelCoord, vec4(oldradianceIn + radianceOut, 0.0));
				#else
					// Normalization is applied deferred.
					int idx = cacheSpecEnvmapTexelCoord.x + cacheSpecEnvmapTexelCoord.y * SPECULARENVMAP_PERCACHESIZE;
					localSpecularEnvmap[idx] = EndcodeRadiance(radianceOut + DecodeRadiance(localSpecularEnvmap[idx]));
				#endif
			#endif
			}
		}
	}

	
	if(gl_GlobalInvocationID.x < TotalLightCacheCount)
	{
		// Save to buffer.
	#ifdef INDDIFFUSE_VIA_H
		LightCacheEntries[gl_GlobalInvocationID.x].irradianceH1 = irradianceHBasis[0];
		LightCacheEntries[gl_GlobalInvocationID.x].irradianceH4r = irradianceHBasis[3].r;
		LightCacheEntries[gl_GlobalInvocationID.x].irradianceH2 = irradianceHBasis[1];
		LightCacheEntries[gl_GlobalInvocationID.x].irradianceH4g = irradianceHBasis[3].g;
		LightCacheEntries[gl_GlobalInvocationID.x].irradianceH3 = irradianceHBasis[2];
		LightCacheEntries[gl_GlobalInvocationID.x].irradianceH4b = irradianceHBasis[3].b;
		#if INDDIFFUSE_VIA_H > 4
		LightCacheEntries[gl_GlobalInvocationID.x].irradianceH5 = irradianceHBasis[4];
		LightCacheEntries[gl_GlobalInvocationID.x].irradianceH6 = irradianceHBasis[5];
		#endif
	#endif

	#if defined(INDDIFFUSE_VIA_SH1) || defined(INDDIFFUSE_VIA_SH2)
		LightCacheEntries[gl_GlobalInvocationID.x].SH1neg1 = SH1neg1;
		LightCacheEntries[gl_GlobalInvocationID.x].SH00_r = SH00.r;
		LightCacheEntries[gl_GlobalInvocationID.x].SH10 = SH10;
		LightCacheEntries[gl_GlobalInvocationID.x].SH00_g = SH00.g;
		LightCacheEntries[gl_GlobalInvocationID.x].SH1pos1 = SH1pos1;
		LightCacheEntries[gl_GlobalInvocationID.x].SH00_b = SH00.b;

		#ifdef INDDIFFUSE_VIA_SH2
		LightCacheEntries[gl_GlobalInvocationID.x].SH2neg2 = SH2neg2;
		LightCacheEntries[gl_GlobalInvocationID.x].SH20_r = SH20.r;
		LightCacheEntries[gl_GlobalInvocationID.x].SH2neg1 = SH2neg1;
		LightCacheEntries[gl_GlobalInvocationID.x].SH20_g = SH20.g;
		LightCacheEntries[gl_GlobalInvocationID.x].SH2pos1 = SH2pos1;
		LightCacheEntries[gl_GlobalInvocationID.x].SH20_b = SH20.b;
		LightCacheEntries[gl_GlobalInvocationID.x].SH2pos2 = SH2pos2;
		#endif
	#endif

	#ifdef INDIRECT_SPECULAR
	#ifndef DIRECT_SPECULAR_MAP_WRITE
		for(int x=0; x<SPECULARENVMAP_PERCACHESIZE; ++x)
			for(int y=0; y<SPECULARENVMAP_PERCACHESIZE; ++y)
			{
				vec3 radianceIn = DecodeRadiance(localSpecularEnvmap[x + y * SPECULARENVMAP_PERCACHESIZE]) * specularEnvMapBaseNormalization;
				imageStore(CacheSpecularEnvmap, cacheSpecularEnvmapOffset + ivec2(x,y), vec4(radianceIn, 0.0));
			}

//		for(int i=0; i< SPECULARENVMAP_PERCACHESIZE*SPECULARENVMAP_PERCACHESIZE; ++i)
//			imageStore(CacheSpecularEnvmap, ivec2(cacheSpecularEnvmapOffset + Morton_2D_Decode_16bit(i)), vec4(DecodeRadiance(localSpecularEnvmap[i]), 0.0));
				
	#endif
	#endif
	}
}